/**
 *	Mode Lighting
 *
 *	Bruce Ravenel
 *	Date: 2015-05-08
 */
definition(
    name: "Mode Lighting",
    namespace: "bravenel",
    author: "Bruce Ravenel",
    description: "Set Dimmer Levels Based on Mode, with Motion on/off",
    category: "Convenience",
    iconUrl: "https://s3.amazonaws.com/smartapp-icons/MyApps/Cat-MyApps.png",
    iconX2Url: "https://s3.amazonaws.com/smartapp-icons/MyApps/Cat-MyApps@2x.png"
)

preferences {
    page(name: "selectDimmers")
    page(name: "motionSettings")
    page(name: "otherSettings")
	
	page(name: "timeIntervalInput", title: "Only during a certain time") {
		section {
			input "starting", "time", title: "Starting", required: false
			input "ending", "time", title: "Ending", required: false
		}
	}
}

def selectDimmers() {
	dynamicPage(name: "selectDimmers", title: "Select your dimmers", nextPage: "motionSettings", uninstall: true) {

		section("When this...") { 
			input "master", "capability.switchLevel", multiple: false, title: "Master Dimmer Is turned on...", required: true
		}

		section("Then the Master and these Dimmers...") {
			input "dimmers", "capability.switchLevel", multiple: true, title: "Dimmer(s) will be set...", required: false
		}
        
		section("Using these three modes and dimLevels") {
        	input "modes3", "mode", multiple: true, title: "Which modes?", required: true//, defaultValue: ["Home", "Evening", "Night"]
			input "digits6", "number", title: "Use two digits per mode, like\n80|45|01, but as a 6 digit number", required: true, defaultValue: 903010
        }
	}
}

def motionSettings() {
	dynamicPage(name:"motionSettings",title: "Select any motion sensors", nextPage: "otherSettings", uninstall: false) {
    
		section("Turn them On when there is motion...") {
			input "motions", "capability.motionSensor", title: "Motion here", required: false, multiple: true
		}
        
		section("Turn them Off when there is no motion...") {
			input "offMotions", "capability.motionSensor", title: "No motion here", required: false, multiple: true
			input "minutes", "number", title: "For this many minutes", required: false, multiple: false
		}
	
		section(title: "Motion options", hidden: hideOptionsSection(), hideable: true) {

			def timeLabel = timeIntervalLabel()

			href "timeIntervalInput", title: "Only during a certain time", description: timeLabel ?: "Tap to set", state: timeLabel ? "complete" : null

			input "days", "enum", title: "Only on certain days of the week", multiple: true, required: false,
				options: ["Monday", "Tuesday", "Wednesday", "Thursday", "Friday", "Saturday", "Sunday"]

			input "modes", "mode", title: "Only when mode is", multiple: true, required: false
   		}    
	}
}

def otherSettings() {
	dynamicPage(name:"otherSettings", uninstall: false, install: true) {

		section("Turn them on with this button...") {
			input "button", "capability.momentary", title: "Button to turn on", multiple: false, required: false
		}

		section("These switches will be turned off...") {
			input "offSwitches", "capability.switch", title: "When the master is turned off", multiple: true, required: false
		}
        
		section {
			label title: "Assign a name:", required: false
		}
   	}
}


def installed() {
	initialize()
}

def updated() {
	unsubscribe()
	initialize()
}


def initialize() {
	subscribe(master, "switch.on", switchOnHandler)
	subscribe(master, "switch.off", switchOffHandler)
	subscribe(master, "level", levelHandler)
	dimmers.each {subscribe(it, "level", levelHandler)}
	subscribe(location, modeChangeHandler)
	subscribe(motions, "motion.active", motionOnHandler)
	subscribe(offMotions, "motion.inactive", motionOffHandler)
	subscribe(button, "switch.on", buttonOnHandler)
//	
//	this part converts the 6 digit number into the three dimLevels
//
	def digits4 = digits6 % 10000
	def nightLevel = digits4 % 100
	def eveningLevel = (digits4 / 100).toInteger()
	def dayLevel = (digits6 / 10000).toInteger()
	state.modeLevels = [(modes3[0]):dayLevel, (modes3[1]):eveningLevel, (modes3[2]):nightLevel]
    
	if(location.mode in modes3) {
    	state.currentMode = location.mode
        state.dimLevel = state.modeLevels[location.mode]
    } else {
    	state.currentMode = modes3[0]
        state.dimLevel = dayLevel
    }
	state.motionOffDismissed = true
	state.masterOff = true
}

def switchesOn(condition, level) {
	state.motionOffDismissed = true    		// use this variable instead of unschedule() to kill pending off()
    if(condition) {
		state.masterOff = false            	// track the on/off state of the master dimmer for efficiency with motionOn
		master.setLevel(level)
		dimmers?.setLevel(level)
    }
}

def switchOnHandler(evt) {
	switchesOn(evt.isPhysical(), state.dimLevel)
}

def buttonOnHandler(evt) {
	switchesOn(true, state.dimLevel)
}

def motionOnHandler(evt) {
	if(allOk) switchesOn(state.masterOff, state.dimLevel)
}

def levelHandler(evt) {      				// allows a dimmer to change the current dimLevel
	switchesOn(true, evt.value)
}

def switchOffHandler(evt) {
	state.masterOff = true
	if(evt.isDigital()) return
	if(motions) dimmers?.off() else {  
		if(master.currentLevel != state.dimLevel) master.resetLevel(state.dimLevel) 
		dimmers.each {if(it.currentLevel != state.dimLevel) it.resetLevel(state.dimLevel) else it.off()}
	}
	offSwitches?.off()
}

def switchesOff() {                    		
	if(state.motionOffDismissed) return     // intervening On, kill this Off. Replaces use of unschedule()
    if(allOk) {
		state.masterOff = true
		master.off()
		dimmers?.off()
		offSwitches?.off()
    }
}

def motionOffHandler(evt) {  				// called when motion goes inactive, check all sensors
	if(allOk) {
		def noMotion = true
		offMotions.each {noMotion = noMotion && it.currentMotion == "inactive"}
		if(noMotion) {
			state.motionOffDismissed = false
			if(minutes) runIn(minutes*60, switchesOff) else switchesOff()
		}
	}
}

def modeChangeHandler(evt) {
    if(state.currentMode == location.mode || !(location.mode in modes3)) return		// not one of our 3 modes
    
    state.dimLevel = state.modeLevels[location.mode]
    state.currentMode = location.mode			   
    
    // the next two lines brighten any lights on when new mode is brighter than previous
    
	if(master.currentSwitch == "on" && master.currentLevel < state.dimLevel) master.setLevel(state.dimLevel)
	dimmers.each {if(it.currentSwitch == "on" && it.currentLevel < state.dimLevel) it.setLevel(state.dimLevel)}

	if(motions) return
    
    // for rooms with no motion, reset the dimLevel IF off
    
	if(master.currentSwitch == "off") master.resetLevel(state.dimLevel)
	dimmers.each {if(it.currentSwitch == "off") it.resetLevel(state.dimLevel)}
}

// execution filter methods
private getAllOk() {
	modeOk && daysOk && timeOk
}

private getModeOk() {
	def result = !modes || modes.contains(location.mode)
//	log.trace "modeOk = $result"
	result
}

private getDaysOk() {
	def result = true
	if (days) {
		def df = new java.text.SimpleDateFormat("EEEE")
		if (location.timeZone) {
			df.setTimeZone(location.timeZone)
		}
		else {
			df.setTimeZone(TimeZone.getTimeZone("America/New_York"))
		}
		def day = df.format(new Date())
		result = days.contains(day)
	}
//	log.trace "daysOk = $result"
	result
}

private getTimeOk() {
	def result = true
	if (starting && ending) {
		def currTime = now()
//		def start = timeToday(starting).time
		def start = timeToday(starting,location.timeZone).time
//		def stop = timeToday(ending).time
		def stop = timeToday(ending,location.timeZone).time
		result = start < stop ? currTime >= start && currTime <= stop : currTime <= stop || currTime >= start
	}
//	log.trace "timeOk = $result"
	result
}

private hhmm(time, fmt = "h:mm a")
{
	def t = timeToday(time, location.timeZone)
	def f = new java.text.SimpleDateFormat(fmt)
	f.setTimeZone(location.timeZone ?: timeZone(time))
	f.format(t)
}

private hideOptionsSection() {
	(starting || ending || days || modes) ? false : true
}

private timeIntervalLabel() {
	(starting && ending) ? hhmm(starting) + "-" + hhmm(ending, "h:mm a z") : ""
}
